buildscript {
	ext.kotlin_version = '1.0.0-beta-4584'
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}
plugins {
	id 'application'
}
apply plugin: "kotlin"

project.version = '1.1-SNAPSHOT'
project.group = 'me.nallar'
mainClassName = "me.nallar.JarOpener"

// Extended project information
ext.projectName = 'JarOpener'
ext.inceptionYear = '2016'
ext.packaging = 'jar'
ext.url = 'https://github.com/nallar/JarOpener'
ext.description = 'Opens up jar files for easy monkey-patching'
ext.organization = 'nallar.me'

// Define variables
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

// Source compiler configuration
tasks.withType(JavaCompile) {
	options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
	options.deprecation = true
	options.encoding = 'utf8'
}

repositories {
	jcenter()
	maven { url='http://repo.nallar.me/' }
}

configurations {
	provided
	compile.extendsFrom provided
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile 'org.ow2.asm:asm:5.0.4'
	compile 'org.ow2.asm:asm-tree:5.0.4'
	compile 'com.github.javaparser:javaparser-core:2.3.0'
	compile 'me.nallar:JavaTransformer:1.1-SNAPSHOT'
}

jar {
	baseName = project.name
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from((configurations.runtime - configurations.provided).filter { it.exists() }.collect {
		it.isDirectory() ? it : zipTree(it)
	}) {
		exclude "META-INF/**"
	}
}

jar.manifest.mainAttributes(
	"Main-Class": mainClassName,
	"Built-By": System.properties['user.name'],
	"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
	"Implementation-Title": name,
	"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
	"Implementation-Vendor": url)
